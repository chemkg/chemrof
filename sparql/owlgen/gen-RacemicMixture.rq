PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX chem: <https://w3id.org/chemschema/>
PREFIX type: <http://www.wikidata.org/prop/direct/P31>
PREFIX subClassOf: <http://www.wikidata.org/prop/direct/P279>
PREFIX Isotope: <http://www.wikidata.org/entity/Q25276>
PREFIX Element: <http://www.wikidata.org/entity/Q11344>
PREFIX symbol: <http://www.wikidata.org/prop/direct/P246>
PREFIX atomic_number: <http://www.wikidata.org/prop/direct/P1086>
PREFIX neutron_number: <http://www.wikidata.org/prop/direct/P1148>

CONSTRUCT {
    ?cls a owl:Class ;
         rdfs:label ?label ;
         owl:equivalentClass [
            owl:intersectionOf (
              chem:RacemicMixture
              [
                 a owl:Restriction ;
                 owl:onProperty chem:has_left_enantiomer ;
                 owl:someValuesFrom ?l
              ]
              [
                 a owl:Restriction ;
                 owl:onProperty chem:has_right_enantiomer ;
                 owl:someValuesFrom ?r
              ]
             )
         ] ;
         owl:equivalentClass [
            owl:intersectionOf (
              chem:RacemicMixture [
                 a owl:Restriction ;
                 owl:onProperty chem:chirality_agnostic_form ;
                 owl:someValuesFrom ?p
                 ]
             )
         ]
}
WHERE {
    ?cls a chem:RacemicMixture ;
         rdfs:label ?label ;
         chem:has_left_enantiomer ?l ;
         chem:has_right_enantiomer ?r ;
         chem:chirality_agnostic_form ?p

}
