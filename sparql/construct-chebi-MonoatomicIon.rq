PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX chem: <https://w3id.org/chemschema/>
PREFIX CHEBI: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX chebigraph: <http://purl.obolibrary.org/obo/merged/CHEBI>
PREFIX chebivoc: <http://purl.obolibrary.org/obo/chebi/>
PREFIX ElementInstance: <https://w3id.org/chemschema/ChemicalElement/>
PREFIX MonoatomicIonInstance: <https://w3id.org/chemschema/MonoatomicIon/>

CONSTRUCT {
    ?iri a chem:MonoatomicIon ;
      rdfs:label ?label ;
      chem:charge ?charge_int ;
      chem:has_element ?element_iri ;
      chem:inchi_string ?inchi ;
      chem:chebi_iri ?i
}
WHERE {
  GRAPH chebigraph: {
    ?i rdfs:subClassOf+ CHEBI:24867 ;
       rdfs:label ?label ;
       chebivoc:charge ?charge ;
       chebivoc:formula ?element_symbol ;
       chebivoc:inchi ?inchi .


    FILTER( regex(str(?inchi), "^InChI=1S/\\w+/q.\\d+$") )
    BIND(xsd:integer(?charge) AS ?charge_int)
    BIND(IRI(CONCAT(STR(ElementInstance:),
                    ENCODE_FOR_URI(?element_symbol))) AS ?element_iri)
    BIND(IRI(CONCAT(STR(MonoatomicIonInstance:),
                    ENCODE_FOR_URI(CONCAT(?element_symbol, "-", STR(?charge))))) AS ?iri)
  }
}
