PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX chem: <https://w3id.org/chemschema/>
PREFIX type: <http://www.wikidata.org/prop/direct/P31>
PREFIX WdElement: <http://www.wikidata.org/entity/Q11344>
PREFIX symbol: <http://www.wikidata.org/prop/direct/P246>
PREFIX atomic_number: <http://www.wikidata.org/prop/direct/P1086>
PREFIX ElementInstance: <https://w3id.org/chemschema/ChemicalElement/>

CONSTRUCT {
    ?iri a chem:ChemicalElement ;
      rdfs:label ?label ;
      chem:symbol ?symbol ;
      chem:atomic_number ?an
}
WHERE {
    ?i type: WdElement: ;
       rdfs:label ?label ;
       symbol: ?symbol ;
       atomic_number: ?an .

       FILTER (LANG(?label) = "en")

   BIND(IRI(CONCAT(STR(ElementInstance:), ENCODE_FOR_URI(?symbol))) AS ?iri)
}
