PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX chem: <https://w3id.org/chemschema/>
PREFIX type: <http://www.wikidata.org/prop/direct/P31>
PREFIX subClassOf: <http://www.wikidata.org/prop/direct/P279>
PREFIX Isotope: <http://www.wikidata.org/entity/Q25276>
PREFIX Element: <http://www.wikidata.org/entity/Q11344>
PREFIX symbol: <http://www.wikidata.org/prop/direct/P246>
PREFIX atomic_number: <http://www.wikidata.org/prop/direct/P1086>
PREFIX neutron_number: <http://www.wikidata.org/prop/direct/P1148>
PREFIX ElementInstance: <https://w3id.org/chemschema/ChemicalElement/>
PREFIX IsotopeInstance: <https://w3id.org/chemschema/Isotope/>

CONSTRUCT {
    ?iri a chem:Isotope ;
      rdfs:label ?label ;
      chem:neutron_number ?nn ;
      chem:has_element ?element_iri
}
WHERE {
    ?i (type:/subClassOf:*) Isotope: ;
       rdfs:label ?label ;
       atomic_number: ?an ;
       neutron_number: ?nn .
    ?e type: Element: ;
       atomic_number: ?an ;
       symbol: ?element_symbol .

    FILTER (LANG(?label) = "en")

   BIND(IRI(CONCAT(STR(ElementInstance:),
                   ENCODE_FOR_URI(?element_symbol))) AS ?element_iri)
   BIND(IRI(CONCAT(STR(IsotopeInstance:),
                   ENCODE_FOR_URI(CONCAT(?element_symbol, "-", STR(?nn))))) AS ?iri)

}
